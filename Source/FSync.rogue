#===============================================================================
# FSync.rogue
# November 4, 2020
#===============================================================================

$requireRogue "1.7.7"

$define RELEASE_VERSION "1.0"
$define RELEASE_DATE    "November 4, 2020"

uses Utility/CommandLineParser
try
  FSync( System.command_line_arguments )
catch (error:Error)
  Console.error.println "="*80
  Console.error.println "ERROR"
  Console.error.println error->String.word_wrapped(78).indented(2)
  Console.error.println "="*80
  System.exit 1
endTry

class FSync
  METHODS
    method init( args:String[] )
      local command = CommandLineParser().
      [
        option( "--dry",   &aliases=["--dry-run","-d"] )
        option( "--help",  &aliases=["-h","-?"] )
        option( "--keep",  &aliases=["--keep-unused","-k"] )
        option( "--quiet", &alias="-q" )
      ].parse( System.command_line_arguments )

      if (command//options//help)
        print_usage
        System.exit
      endIf

      local destination_filepath = "."
      which (command//args.count)
        case 0
          print_usage
          System.exit
        case 1
          noAction
        case 2
          destination_filepath = command//args[1]->String
        others
          throw Error( "Too many filepaths given." )
      endWhich

      local source_filepath = command//args[0]->String

      if (not File.exists(source_filepath))
        throw Error( "Source filepath does not exist: " + source_filepath )
      endIf

      if (File.is_folder(source_filepath))
        if (not File.is_folder(destination_filepath))
          File.create_folder( destination_filepath )
          if (not File.is_folder(destination_filepath))
            throw Error( "Unable to create folder " + destination_filepath )
          endIf
        endIf
        File.sync_from( source_filepath, destination_filepath, &verbose=not command//options//quiet,
          &dry_run=command//options//dry->Logical, &keep_unused=command//options//keep->Logical )

      else
        if (File.is_folder(destination_filepath))
          destination_filepath = File.ensure_ends_with_separator( destination_filepath )
          destination_filepath += File.filename(source_filepath)
        endIf
        if (File.is_different_than(source_filepath,destination_filepath))
          if (not command//options//quiet) println "$ -> $" (source_filepath,destination_filepath)
          if (not command//options//dry)   File.copy( source_filepath, destination_filepath )
        endIf
      endIf

    method print_usage
      println "=" * 80
      println "FSync v$"        (RELEASE_VERSION)
      println "=" * 80
      println "$ by Abe Pralle" (RELEASE_DATE)
      println @|
               |USAGE
               |  fsync [options] source-filepath [destination-filepath]
               |
               |OPTIONS
               |  --dry-run, --dry, -d
               |    Print file copy messages without actually copying them.
               |    Should not be used with --quiet.
               |
               |  --keep-unused, --keep, -k
               |    Do not delete files that are in destination but no longer in source.
               |
               |  --quiet, -q
               |    Do not print file copy actions.
endClass
